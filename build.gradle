plugins {
    id 'org.jetbrains.intellij' version '0.4.20'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'me.lensvol'
version '0.3.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.moandjiezana.toml:toml4j:0.7.2"

    compile('io.sentry:sentry:1.7.30') {
        exclude group: 'org.slf4j'
    }
}

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    pluginName 'intellij-blackconnect'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    version project.version
    changeNotes """
      <p>0.3.1</p>
      <ul>
        <li>Fix default line length being set to 80 instead of 88 (reported by Piotr Mazur).</li>
      </ul>

      <p>0.3.0</p>
      <ul>
        <li>Load formatting settings from <b>pyproject.toml</b>.</li>
        <li>Fix file being reformatted multiple times if several projects are opened.</li>
        <li>Improve reporting of plugin crashes.</li>
      </ul>
      """
}

processResources {
    def properties = ['version': project.version]
    inputs.properties(properties)
    filesMatching('version.properties') {
        expand(properties)
    }
}