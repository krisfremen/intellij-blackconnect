plugins {
    id 'org.jetbrains.intellij' version '0.4.20'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'me.lensvol'
version '0.4.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.moandjiezana.toml:toml4j:0.7.2"

    testCompile 'org.mock-server:mockserver-netty:5.3.0'

    compile('io.sentry:sentry:1.7.30') {
        exclude group: 'org.slf4j'
    }
}

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    pluginName 'intellij-blackconnect'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    version project.version
    changeNotes """
      <p>0.4.1</p>
      <ul>
        <li>Fix regression with file save trigger reformatting non-Python files (reported by Matthew R. Scott).</li>
      </ul>

      <p>0.4.0</p>
      <ul>
        <li>Added "Check connection" button to "Settings" screen.</li> 
        <li>Added "Reformat Selected Fragment" action to support partial reformatting.</li>
        <li>Reformatting actions moved to <b>BlackConnect</b> submenu under "Tools".</li>
        <li>Fix rare crash when closing tab with window handle set to <i>null</i>.</li>
        <li>Notification balloons no longer show up in "Event log".</li>
      </ul>
      """
}

processResources {
    def properties = ['version': project.version]
    inputs.properties(properties)
    filesMatching('version.properties') {
        expand(properties)
    }
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}